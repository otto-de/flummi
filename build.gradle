buildscript {
    
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

plugins {
    id 'maven'
    id 'signing'
    id 'java'
    id 'eclipse'
    id 'idea'
    id "com.jfrog.artifactory" version "4.4.10"
}


group = 'de.otto'
archivesBaseName='flummi'
version='5.0.24.0'

if (project.hasProperty('isSnapshot')) {
    version += '-SNAPSHOT'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
    scriptFile = "${project.projectDir}/bin/gradlew"
    jarFile = "${project.projectDir}/bin/gradle/wrapper/gradle-wrapper.jar"
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}


test {
    systemProperties = System.properties

    description "Tell gradle to use TestNG with all groups"
    useTestNG()
    options {
        useDefaultListeners = true
    }
}

idea.project {
    jdkName = "1.8"
    languageLevel = "1.8"
    ipr.withXml { provider ->
        provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

if (!project.hasProperty('isSnapshot')) {
    signing {
        sign configurations.archives
    }
}

dependencies {
    compile "com.google.code.gson:gson:2.8.0"
    compile "com.ning:async-http-client:1.9.40"
    compile "org.slf4j:slf4j-api:1.7.22"

    testCompile "org.testng:testng:6.9.10"
    testCompile "org.hamcrest:hamcrest-core:1.3"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile "org.mockito:mockito-core:2.5.3"

    // runtime dependencies of mockito
    testRuntime "net.bytebuddy:byte-buddy:1.6.0"
    testRuntime "org.objenesis:objenesis:2.4"
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.properties["sonatypeUsername"], password: project.properties["sonatypePassword"])
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.properties["sonatypeUsername"], password: project.properties["sonatypePassword"])
            }

            pom.project {
                name 'flummi'
                packaging 'jar'
                description 'Helper library for elasticsearch'
                url 'http://github.com/otto-de/flummi'

                scm {
                    url 'scm:git@github.com:otto-de/flummi.git'
                    connection 'scm:git@github.com:otto-de/flummi.git'
                    developerConnection 'scm:git@github.com:otto-de/flummi.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'bvoigt'
                        name 'Bastian Voigt'
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = project.properties["artifactoryUrl"]
    //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            if (project.hasProperty('isSnapshot')) {
                repoKey = 'maven-san-snapshots-local'
            } else {
                repoKey = 'maven-san-releases-local'
            }
            username = project.properties["artifactoryUsername"]
            password = project.properties["artifactoryPassword"]
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publishConfigs('archives')
            publishArtifacts = true
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
            publishIvy = false
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release-virtual'
        }
    }
}
